COPY	START	0		.COPY FILE FROM INPUT TO OUTPUT
FIRST	STL		RETADR	.17202D
		LDB		#LENGTH	.69202d
		BASE	LENGTH

CLOOP	+JSUB	RDREC	.4b101036
		LDA		LENGTH	.032026
		COMP	#0		.290000
		JEQ		ENDFIL	.332007
		+JSUB	WRREC	.4b10105d
		J		CLOOP	.3f2fec
ENDFIL	LDA		EOF		.032010
		STA		BUFFER	.0f2016
		LDA		#3		.010003
		STA		LENGTH  .0f200d
		+JSUB	WRREC	.4b10105d
		J		@RETADR	.3e2003
EOF		BYTE	C'EOF'	.454f46	
RETADR	RESW	1
LENGTH	RESW	1		.LENGTH OF RECORD
BUFFER	RESB	4096	.4096-BYTE BUFFER AREA
.
.	SUBROUTINE TO READ RECORD INTO BUFFER
.
RDREC	CLEAR	X		.b410
		CLEAR	A		.b400
		CLEAR	S		.b440
		+LDT	#4096	.75101000
RLOOP	TD		INPUT	.e32019
		JEQ		RLOOP	.332ffa
		RD		INPUT	.db2013
		COMPR	A,S		.a004
		JEQ		EXIT	.332008
		STCH	BUFFER,X .57c003
		TIXR	T		.b850
		JLT		RLOOP	.3b2fea
EXIT	STX		LENGTH	.134000
		RSUB			.4f0000
INPUT	BYTE	X'F1'	.f1
.
.	SUBROUTINE TO WRITE RECORD FROM BUFFER
.
		LDA		=3
WRREC	CLEAR	X		.b410
		LDT		LENGTH	.774000 ali pa 00D?
WLOOP	TD		OUTPUT	.e32011
		JEQ		WLOOP	.332ffa
		LDCH	BUFFER,X .53c003
		WD		OUTPUT	.df2008
		TIXR	T		.b850
		JLT		WLOOP	.3b2fef
		RSUB			.4f0000
OUTPUT	BYTE	X'05'	.05
		END		FIRST